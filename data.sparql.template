SELECT DISTINCT ?subject ?predicate ?object
WITH {
%ENTITIES%
} AS %entities
WITH {
  SELECT (?entity AS ?subject) (?wdt AS ?predicate) ?object WHERE {
    INCLUDE %entities.
    ?property a wikibase:Property;
              wikibase:directClaim ?wdt.
    MINUS { ?property wikibase:propertyType wikibase:ExternalId. }
    ?entity ?wdt ?object.
  }
} AS %directStatements
WITH {
  SELECT DISTINCT ?subject WHERE {
    INCLUDE %entities.
    ?itemProperty a wikibase:Property;
                  wikibase:propertyType wikibase:WikibaseItem;
                  wikibase:directClaim ?itemWdt.
    ?entity ?itemWdt ?subject.
  }
} AS %indirectEntities
WITH {
  SELECT ?subject (?wdt AS ?predicate) ?object WHERE {
    INCLUDE %indirectEntities.
    ?property a wikibase:Property;
              wikibase:directClaim ?wdt.
    MINUS { ?property wikibase:propertyType wikibase:ExternalId. }
    ?subject ?wdt ?object.
  }
} AS %indirectStatements
WITH {
  SELECT (?object AS ?subject_) (wdt:P31 AS ?predicate_) (?class AS ?object_) WHERE {
    INCLUDE %indirectStatements.
    ?object wdt:P31 ?class.
  }
} AS %indirectClasses_
WITH {
  SELECT (?subject_ AS ?subject) (?predicate_ AS ?predicate) (?object_ AS ?object) WHERE {
    INCLUDE %indirectClasses_.
  }
} AS %indirectClasses
WHERE {
  { INCLUDE %directStatements. }
  UNION
  { INCLUDE %indirectStatements. }
  UNION
  { INCLUDE %indirectClasses. }
}
